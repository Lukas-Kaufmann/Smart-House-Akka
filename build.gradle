plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.github.node-gradle.node" version "3.2.1"
}

group 'at.fhv.lab2'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
npm_run_build {
    inputs.files fileTree("src/main/js/smart-house-control-panel/public")
    inputs.files fileTree("src/main/js/smart-house-control-panel/src")

    inputs.file 'src/main/js/smart-house-control-panel/package.json'
    inputs.file 'src/main/js/smart-house-control-panel/package-lock.json'
}

node {
    nodeProjectDir = file("src/main/js/smart-house-control-panel")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'com.typesafe.akka:akka-actor-typed_2.13:2.6.10'
    implementation 'com.typesafe.akka:akka-actor-testkit-typed_2.13:2.6.10'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

application {
    mainClass = 'at.fhv.sysarch.lab2.HomeAutomationSystem'
}

run {
    standardInput = System.in
}

tasks.register("copyFrontEnd", Copy) {
    dependsOn tasks.npm_run_build
    from layout.projectDirectory.dir("src/main/js/smart-house-control-panel/build")
    into layout.projectDirectory.dir("src/main/resources/static/build")
}

build {
    dependsOn tasks.copyFrontEnd
}

test {
    useJUnitPlatform()
}